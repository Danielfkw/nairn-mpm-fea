! --------- Preamble
Title "FEA Analysis of Cantilever Beam"
Name "FEA Analyst's Name"

Header
  A cantilever beam that is clamped at its base and
  loaded on its free end with a single point load.
EndHeader

! Note: stress in MPa, Load in N
ConsistentUnits "mm","g","ms"

! --------- Parameters
! Dimensions
#length = 50
#depth = 10
#thick = 10

! Mesh density
#horiz=6
#vert=3

! Material type
#matname = "polymer"

! End load
#load=500

! --------- Analysis Type and Materials
Analysis "Plane Stress FEA"
Element "8 Node Quadrilateral"

Material "polymer","Polymer","Isotropic"
  E 2300
  nu .33
  a 60
Done

! --------- Create the Mesh
Area #matname,#thick
  Path "Bottom",#horiz
    Keypoint "BotLeft",0,0
    Keypoint "BotRight",#length,0
  EndPath
  Path "Right",#vert
    Keypoint "BotRight"
    Keypoint "TopRight",#length,#depth
  EndPath
  Path "Top",#horiz
    Keypoint "TopRight"
    Keypoint "TopLeft",0,#depth
  EndPath
  Path "Left",#vert
    Keypoint "TopLeft"
    Keypoint "BotLeft"
  EndPath
EndArea

! --------- Boundary Conditions
Resequence "BotRight"

FixLine 0,0,0,#depth
  Displacement x
  Displacement y
EndFixLine

FixPoint "TopRight"
  Load y,#load
EndFixPoint